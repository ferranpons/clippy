apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'twitter-gradle-plugin'
apply plugin: 'com.novoda.bintray-release'

group = 'com.ferranpons.clippylib'
version = "${versionMajor}.${versionMinor}.${versionPatch}"

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.2'
  resourcePrefix 'clippy_'

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    consumerProguardFiles 'lib-proguard-rules.txt'
    vectorDrawables.useSupportLibrary = true
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      consumerProguardFiles 'lib-proguard-rules.txt'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  dexOptions {
    preDexLibraries !"true".equals(System.getenv("TRAVIS"))
  }

  packagingOptions {
    exclude 'protobuf.meta'
  }

  lintOptions {
    disable 'UnusedResources'
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "org.jetbrains.kotlin:kotlin-reflect:1.2.10"

  def supportVersion = '27.0.2'
  implementation "com.android.support:appcompat-v7:$supportVersion"
  implementation "com.android.support:design:$supportVersion"
  implementation "com.android.support:recyclerview-v7:$supportVersion"
  implementation "com.android.support:cardview-v7:$supportVersion"

  implementation "com.jakewharton.timber:timber:3.1.0"
  implementation "com.fasterxml.jackson.core:jackson-databind:2.8.9"
  implementation "com.fasterxml.jackson.core:jackson-annotations:2.8.9"

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.1'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

publish {
  userOrg = 'ferranpons'
  groupId = 'com.ferranpons'
  artifactId = 'clippylib'
  publishVersion = "${versionMajor}.${versionMinor}.${versionPatch}"
  desc = 'Android library that help you create an Office Assistant (Clippy)'
  website = 'https://github.com/ferranpons'
}

twitterPlugin {
  consumerKey = System.getenv()['TWITTER_API_KEY']
  consumerSecret = System.getenv()['TWITTER_API_SECRET']
  accessToken = System.getenv()['TWITTER_ACCESS_TOKEN']
  accessTokenSecret = System.getenv()['TWITTER_ACCESS_TOKEN_SECRET']
  message = "New Clippy Android Library version ${versionMajor}.${versionMinor}.${versionPatch} available for download. Check out the details at https://github.com/ferranpons/clippy"
}
repositories {
  mavenCentral()
}

def getVersionMajor() {
  def content = file('../version.properties')

  Properties properties = new Properties()
  InputStream is = new ByteArrayInputStream(content.getBytes())
  properties.load(is)

  return properties['MAJOR'].toInteger()
}

def getVersionMinor() {
  def content = file('../version.properties')

  Properties properties = new Properties()
  InputStream is = new ByteArrayInputStream(content.getBytes())
  properties.load(is)

  return properties['MINOR'].toInteger()
}

def getVersionPatch() {
  def content = file('../version.properties')

  Properties properties = new Properties()
  InputStream is = new ByteArrayInputStream(content.getBytes())
  properties.load(is)

  return properties['PATCH'].toInteger()
}
